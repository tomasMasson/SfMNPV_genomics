SAMPLES = ["sfmnpv"]

rule all:
    input:
        "within-host_diversity/sfmnpv_delly_sv.vcf",
        "within-host_diversity/SNV_count.svg",
        "within-host_diversity/SNV_frequency_distribution.svg",
        "horizontal_genetic_transfer/reads_virus_host_te.bam"

rule reads_quality_filter:
    input:
        "data/{sample}_1.fq.gz",
        "data/{sample}_2.fq.gz"
    output:
        "within-host_diversity/{sample}_reads1.fq",
        "within-host_diversity/{sample}_reads2.fq",
        html = "within-host_diversity/{sample}_reads.html",
        json = "within-host_diversity/{sample}_reads.json"
    shell:
        """
        fastp\
        --in1 {input[0]} --in2 {input[1]}\
        --out1 {output[0]} --out2 {output[1]}\
        --html {output.html} --json {output.json}
        """

rule bwa_index:
    input:
        "genome_assembly/{sample}.fa"
    output:
        "within-host_diversity/{sample}.fa",
        "within-host_diversity/{sample}.fa.amb",
        "within-host_diversity/{sample}.fa.ann",
        "within-host_diversity/{sample}.fa.bwt",
        "within-host_diversity/{sample}.fa.pac",
        "within-host_diversity/{sample}.fa.sa"
    params:
        "within-host_diversity/{sample}.fa"
    shell:
        """
        cp {input} {params} &&\
        bwa index {params}\
        """

rule bwa_alignment:
    input:
        "within-host_diversity/sfmnpv.fa",
        "within-host_diversity/sfmnpv_reads1.fq",
        "within-host_diversity/sfmnpv_reads2.fq"
    output:
        "within-host_diversity/reads.bam"
    params:
        rg=r"@RG\tID:sfmnpv\tSM:1\tLB:lib1\tPL:Illumina\tPU:unit1"
    threads: 4
    shell:
        """
        bwa mem -R '{params.rg}' -t {threads} {input} |\
        samtools view -Sb > {output}
        """

rule samtool_sort:
    input:
        "within-host_diversity/reads.bam"
    output:
        "within-host_diversity/reads_sort.bam",
        "within-host_diversity/reads_sort.bam.bai",
    threads: 4
    shell:
        """
        samtools sort -@ {threads} {input} > {output[0]} && \
        samtools index -@ {threads} {output[0]}
        """

rule lofreq_snv_calling:
    input:
        genome="within-host_diversity/sfmnpv.fa",
        bam="within-host_diversity/reads_sort.bam"
    output:
        "within-host_diversity/sfmnpv_snv.vcf"
    threads: 2
    shell:
        """
        samtools faidx {input.genome} &&\
        lofreq call-parallel --pp-threads {threads}\
        -f {input.genome} -o {output} {input.bam}
        """

rule snpEff_snv_annotation:
    input:
        "within-host_diversity/sfmnpv_snv.vcf"
    output:
        "within-host_diversity/sfmnpv_snv_ann.vcf"
    params:
        config="data/snpEff.config",
        db="sfmnpv_argentina",
        seq="within-host_diversity/snpEff_db/sfmnpv_argentina/sequences.fa",
        gtf="within-host_diversity/snpEff_db/sfmnpv_argentina/genes.gtf"
    shell:
        """
        mkdir -p within-host_diversity/snpEff_db/sfmnpv_argentina && \
        cp genome_assembly/sfmnpv.fa {params.seq} && \
        src/add_names_gtf.py annotation/sfmnpv_annotation.gtf data/sfmnpv_protein_table.csv > {params.gtf} && \
        snpEff build -gtf22 -c {params.config} {params.db} && \
        snpEff -c {params.config} {params.db} {input} > {output} &&\
        rm snpEff_*
        """

rule delly_sv_calling:
    input:
        genome="within-host_diversity/sfmnpv.fa",
        bam="within-host_diversity/reads_sort.bam"
    output:
        "within-host_diversity/sfmnpv_delly_sv.vcf"
    shell:
        """
        delly call -g {input.genome} -o {output} {input.bam}
        """

rule plot_snv_frequency:
    input:
        "within-host_diversity/sfmnpv_snv_ann.vcf"
    output:
        "within-host_diversity/SNV_count.svg",
        "within-host_diversity/SNV_frequency_distribution.svg"
    shell:
        """
        python src/variants_frequency_intrahost.py {input} && \
        mv SNV_count.svg {output[0]} && \
        mv SNV_frequency_distribution.svg {output[1]}
        """

rule concatenate_spodoptera_TE:
    input:
        "horizontal_genetic_transfer/Dfam.fna",
        "horizontal_genetic_transfer/sfru.mais.corrected.3.1.fa"
    output:
        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa"
    shell:
        """
        cat {input} > {output}
        """

#rule bwa_index_host_TE_database:
#    input:
#        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa"
#    output:
#        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa.amb",
#        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa.ann",
#        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa.bwt",
#        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa.pac",
#        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa.sa"
#    shell:
#        """
#        bwa index {input}
#        """
#
rule bwa_alignment_integrations:
    input:
        "horizontal_genetic_transfer/sfmnpv_host_te_db.fa",
        "within-host_diversity/sfmnpv_reads1.fq",
        "within-host_diversity/sfmnpv_reads2.fq"
    output:
        "horizontal_genetic_transfer/reads_virus_host_te.bam"
    params:
        rg=r"@RG\tID:sfmnpv\tSM:1\tLB:lib1\tPL:Illumina\tPU:unit1"
    threads: 4
    shell:
        """
        bwa index {input[0]} && \
        bwa mem -R '{params.rg}' -t {threads} {input} |\
        samtools view -Sb > {output}
        """
